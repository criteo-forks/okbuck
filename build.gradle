buildscript {
    apply from: rootProject.file('dependencies.gradle')
    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath deps.build.butterKnifePlugin
        classpath deps.build.retrolambdaPlugin
        classpath deps.build.sqlDelightPlugin
        classpath deps.build.shadowJar
    }
}

allprojects { project ->
    project.apply from: rootProject.file('dependencies.gradle')
    repositories {
        jcenter()
    }
}

apply plugin: 'com.uber.okbuck'

subprojects { project ->
    afterEvaluate {
        if (project.plugins.hasPlugin('java')) {
            addCommonConfigurationForJavaModules(project)

        }
        project.tasks.withType(Test) { Test task ->
            task.jvmArgs << "-Djava.awt.headless=true"
        }
    }
}

def addCommonConfigurationForJavaModules(Project project) {
    if (project.plugins.hasPlugin('me.tatarka.retrolambda')) {
        project.sourceCompatibility = JavaVersion.VERSION_1_8
        project.targetCompatibility = JavaVersion.VERSION_1_8
    } else {
        project.sourceCompatibility = JavaVersion.VERSION_1_7
        project.targetCompatibility = JavaVersion.VERSION_1_7
    }
}

okbuck {
    buckProjects = project.subprojects.findAll { it.name != "plugin" && it.name != "transform-cli" }

    intellij {
        sources = true
    }

    test {
        espresso = true
        robolectric = true
    }

    wrapper {
        watch += ["**/*.sq"]
    }
}

gradle.buildFinished {
    "zip -d .okbuck/cache/org.hamcrest.hamcrest-core-1.3.jar LICENSE.txt".execute()
    "zip -d .okbuck/cache/org.hamcrest.hamcrest-library-1.3.jar LICENSE.txt".execute()
    "zip -d .okbuck/cache/org.hamcrest.hamcrest-integration-1.3.jar LICENSE.txt".execute()
}
